@Ecore(nsURI="edu.uoc.som.jarvis.common")
@GenModel(modelDirectory="/common/src/main/java-gen", importerID="org.eclipse.emf.importer.ecore", complianceLevel="8.0")
package edu.uoc.som.jarvis.common

class Program {
    contains Instruction[] instructions
}

abstract class Instruction {
    
}

abstract class ImportDeclaration extends Instruction {
    String path
    String alias
}

class PlatformImportDeclaration extends ImportDeclaration {

}

class LibraryImportDeclaration extends ImportDeclaration {

}

class VariableDeclaration extends Instruction {
    String name
    contains Expression value
}

abstract class Expression extends Instruction {
    
}

abstract class Operation extends Expression {
    
}

class UnaryOperation extends Operation {
     contains Expression exp
}

class IfExpression extends Expression {
    contains Expression condition
    contains Instruction[] thenInstructions
    contains Instruction[] elseInstructions
}

class VariableAccess extends Expression {
    refers VariableDeclaration referredVariable
}

class ContextAccess extends Expression {
    String contextName
}

class SessionAccess extends Expression {

}

abstract class Literal extends Expression {
    
}

class StringLiteral extends Literal {
    String value
}

class NumberLiteral extends Literal {
    int value
}

class BooleanLiteral extends Literal {
    boolean value
}

class OperationCall extends Expression {
    String name
    contains Expression source
    contains Expression[] args
}

class PropertyAccess extends Expression {
    String feature
    contains Expression source
}